plugins {
  id 'java'
}

defaultTasks 'build'

sourceCompatibility = '17'
targetCompatibility = '17'
version = '2.0'

dependencies {
  implementation 'com.github.roborescue:rcrs-server:protobuf-v6-SNAPSHOT'
  implementation 'com.github.roborescue:rcrs-adf-core:protobuf-comm-SNAPSHOT'

  implementation 'com.google.common:google-collect:0.5'
  implementation 'log4j:log4j:1.2.17'

  testImplementation 'junit:junit:4.12'
}

repositories {
  mavenCentral()

  maven {
    url = 'http://sourceforge.net/projects/jsi/files/m2_repo'
    allowInsecureProtocol = true
  }
  maven {
    url = 'https://repo.enonic.com/public/'
    allowInsecureProtocol = true
  }
  maven {
    url 'https://jitpack.io'
  }
}

test {
  useJUnit()
}

javadoc {
  options {
    encoding = 'UTF-8'
    addStringOption('Xdoclint:none', '-quiet')
  }
}

sourceSets {
  main {
    java {
      srcDirs = [ 'src' ]
    }
  }
}

clean.doFirst {
  delete file( new File( rootDir, 'bin' ) )
  delete file( new File( rootDir, 'build' ) )
  delete file( new File( rootDir, 'precomp_data' ) )
  delete file( new File( rootDir, 'test-results' ) )
}

[compileJava, compileTestJava].each {
  it.options.encoding = 'UTF-8'
  it.options.compilerArgs += [ '-Xlint:unchecked', '-Xlint:deprecation' ]
}

task launch(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'adf.Main'
  maxHeapSize = '2048m'
}